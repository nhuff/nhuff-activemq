# Class: activemq
#
# This module manages the ActiveMQ messaging middleware.
#
# Parameters:
#
# Actions:
#
# Requires:
#
#   Class['java']
#
# Sample Usage:
#
# node default {
#   class { 'activemq': }
# }
#
# To supply your own configuration file:
#
# node default {
#   class { 'activemq':
#     server_config => template('site/activemq.xml.erb'),
#   }
# }
#
class activemq(
  $version       = 'present',
  $ensure        = 'running',
  $webconsole    = true,
  $memory_usage  = '50 mb',
  $store_usage   = '1 gb',
  $temp_usage    = '500 mb'
) {

  validate_re($ensure, '^running$|^stopped$')
  validate_re($version, '^present$|^latest$|^[._0-9a-zA-Z:-]+$')
  validate_bool($webconsole)

  $version_real = $version
  $ensure_real  = $ensure
  $webconsole_real = $webconsole


  # Anchors for containing the implementation class
  anchor { 'activemq::begin':
    before => Class['activemq::packages'],
    notify => Class['activemq::service'],
  }

  class { 'activemq::packages':
    version => $version_real,
    notify  => Class['activemq::service'],
  }

  class { 'activemq::config':
    webconsole    => $webconsole_real,
    memory_usage  => $memory_usage,
    store_usage   => $store_usage,
    temp_usage    => $temp_usage,
    require       => Class['activemq::packages'],
    notify        => Class['activemq::service'],
  }

  class { 'activemq::service':
    ensure => $ensure_real,
  }

  anchor { 'activemq::end':
    require => Class['activemq::service'],
  }

}

